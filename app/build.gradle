apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion targetSdk

    defaultConfig {
        applicationId "com.rebbit.app"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner'
    }

    flavorDimensions "data"
    productFlavors {
        mock {
            dimension "data"
            applicationIdSuffix ".mock"
        }
        api {
            dimension "data"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        mock.java.srcDirs += 'src/mock/kotlin'
        api.java.srcDirs += 'src/api/kotlin'
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        pickFirst 'mockito-extensions/org.mockito.plugins.MockMaker'
    }
}

kapt {
    javacOptions {
        option("-Xmaxerrs", 500)
    }
}

dependencies {
    implementation project(':data')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    kapt "androidx.databinding:databinding-compiler:$android_gradle_version"

    implementation "android.arch.paging:runtime:$paging_version"
    implementation "android.arch.paging:rxjava2:1.0.0-rc1"

    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:reactivestreams:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'

    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    testImplementation "android.arch.core:core-testing:$lifecycle_version"
    testImplementation "android.arch.paging:common:$paging_version"

    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger_version"

    androidTestImplementation project(':testutil')
    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
    androidTestImplementation "com.android.support.test:runner:$test_support_version"
    androidTestImplementation "com.android.support.test:rules:$test_support_version"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    androidTestImplementation 'com.github.tmurakami:dexopener:0.12.1'

    mockImplementation project(':testutil')
}
